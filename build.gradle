plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    // Apply the application plugin to add support for building a CLI application.
    id 'application'
    id 'eclipse'
}

repositories {
    jcenter()
    maven {
        url "https://dl.bintray.com/jmonkeyengine/org.jmonkeyengine"
        url "https://dl.bintray.com/stephengold/jme3utilities" 
    }
}

//allow vscode -> buildship -> f5 to read the generated assets.jar
sourceSets {
    main {
        resources {
            srcDirs "build/assets/libs/"
        }
    }
}

project(":assets") {
    apply plugin: "java"
    
    buildDir = rootProject.file("build/assets")
    
    sourceSets {
        main {
            resources {
                srcDir '.'
                include '**'
                exclude '**/*.blend'
                exclude '**/*.ps1'
                exclude '**/*.py'
                exclude '**/*.blend1'
                exclude 'bin/*'
            }
        }
    }
}

def jme3Version = '3.3.0-stable'
dependencies {
    implementation 'com.google.guava:guava:28.0-jre'

    implementation "org.jmonkeyengine:jme3-bullet:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-bullet-native:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-core:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-desktop:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-effects:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-lwjgl:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-plugins:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-terrain:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-jogg:${jme3Version}"
    
    implementation 'org.yaml:snakeyaml:1.25'
    implementation 'com.simsilica:lemur:1.13.0'

    implementation group: 'io.jenetics', name: 'jenetics', version: '6.2.0'

    // Use JUnit Jupiter API for testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.1'

    runtimeOnly project(':assets')
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

application {
    // Define the main class for the application
    mainClassName = 'rallygame.game.Main'
}

// run the 'real' game
task runDuel(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'rallygame.duel.DuelApp'
}

task createFatJar(type: Jar) {
    archiveBaseName = 'rallyGame'

    manifest {
        attributes 'Main-Class': 'rallygame.game.Main'
    }

    from sourceSets.main.output
    dependsOn configurations.compileClasspath

    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    dependsOn 'build'
}

task createDuelFatJar(type: Jar) {
    archiveBaseName = 'duel'
    manifest {
        attributes 'Main-Class': 'rallygame.duel.DuelApp'
    }

    from sourceSets.main.output
    dependsOn configurations.compileClasspath

    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    dependsOn 'build'
}


task cleanBlendGltfFiles(type: Delete) {
    delete fileTree('assets') {
        include '**/*.blend.glb'
    }
}

def blenderCommandWin = "C:\\Program Files\\Blender Foundation\\Blender 2.82\\blender"
def blenderCommandLinux = "/usr/bin/blender"
def blenderPythonFile = "$projectDir/assets/blend2gltf.py"

task convertAllBlendFiles {
    doFirst {
        def blenderCommand = blenderCommandLinux
        print('Running on: ' + System.getProperty('os.name').toLowerCase(Locale.ROOT)+'\n')
        def isWindows = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')
        if (isWindows) {
            blenderCommand = blenderCommandWin + '.exe'
        }
        
        // and check blender exists first
        assert file(blenderCommand).exists()

        ConfigurableFileTree files = fileTree(dir: 'assets', include: '**/*.blend')
        
        files.each { File file ->
            File glbFile = new File(file.path + '.glb')
            if (!glbFile.exists() || file.lastModified() > glbFile.lastModified()) {
                println 'Running: ' + file.path
                def ip = new ByteArrayOutputStream()
                exec {
                    standardOutput = ip
                    ignoreExitValue = true
                    def command = '"' + blenderCommand + '" "'+file.path+'" --background --python "' + blenderPythonFile + '"'
                    if (isWindows) {
                        commandLine 'cmd', '/s', '/c', '"'+command+'"'
                    } else {
                        commandLine 'sh', '-c', command
                    }
                }
            } else {
                println 'generated is newer for: ' + file.name
            }
        }
    }
}

compileJava.dependsOn(convertAllBlendFiles)
