plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    // Apply the application plugin to add support for building a CLI application.
    id 'application'
    id 'eclipse'

    id 'edu.sc.seis.launch4j' version '2.5.3'
}

sourceCompatibility = 11

repositories {
    jcenter()
    mavenCentral()
}

launch4j {
    mainClassName = 'survival.SurvivalApp'
    bundledJrePath = 'jre'
    outfile = 'survival.exe'
    // please run copyjre first
    // look in RallyGame\build\launch4j for the lib, jre and exe files, usually i zip and upload
}

def jme3Version = '3.3.0-stable'
dependencies {
    implementation 'com.google.guava:guava:28.0-jre'

    implementation "org.jmonkeyengine:jme3-bullet:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-bullet-native:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-core:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-desktop:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-effects:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-lwjgl:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-plugins:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-terrain:${jme3Version}"
    implementation "org.jmonkeyengine:jme3-jogg:${jme3Version}"
    
    implementation 'org.yaml:snakeyaml:1.25'
    implementation 'com.simsilica:lemur:1.13.0'

    // Use JUnit Jupiter API for testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.1'
}


test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

application {
    // Define the main class for the application
    mainClassName = 'rallygame.game.Main'
}

// run the 'real' game
task runDuel(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'rallygame.duel.DuelApp'
}

task runSurvival(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'survival.SurvivalApp'
}

def blenderCommandWin = "C:\\Program Files\\Blender Foundation\\Blender 2.82\\blender"
def blenderCommandLinux = "/usr/bin/blender"
def blenderPythonFile = "$projectDir/src/main/resources/blend2gltf.py"
task convertAllBlendFiles {
    doFirst {
        def blenderCommand = blenderCommandLinux
        print('Running on: ' + System.getProperty('os.name').toLowerCase(Locale.ROOT)+'\n')
        def isWindows = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')
        if (isWindows) {
            blenderCommand = blenderCommandWin + '.exe'
        }
        
        // and check blender exists first
        assert file(blenderCommand).exists()

        ConfigurableFileTree files = fileTree(dir: 'src/main/resources', include: '**/*.blend')
        
        files.each { File file ->
            File glbFile = new File(file.path + '.glb')
            if (!glbFile.exists() || file.lastModified() > glbFile.lastModified()) {
                println 'Running: ' + file.path
                def ip = new ByteArrayOutputStream()
                exec {
                    standardOutput = ip
                    ignoreExitValue = true
                    def command = '"' + blenderCommand + '" "'+file.path+'" --background --python "' + blenderPythonFile + '"'
                    if (isWindows) {
                        commandLine 'cmd', '/s', '/c', '"'+command+'"'
                    } else {
                        commandLine 'sh', '-c', command
                    }
                }
            } else {
                println 'generated is newer for: ' + file.name
            }
        }
    }
}

compileJava.dependsOn(convertAllBlendFiles)

task copyjre {
    // TODO get this to run before launch4j.createExe
    doFirst {
        copy {
            from "C:\\Program Files\\AdoptOpenJDK\\jdk-11.0.7.10-hotspot"
            into "build/launch4j/jre"
        }
    }
}
